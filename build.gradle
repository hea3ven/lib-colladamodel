buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.1-SNAPSHOT'
    }
}

apply plugin: 'forge'

sourceCompatibility = 1.6

version = "1.0a1"
group = "com.hea3ven" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "lib-colladamodel"

minecraft {
    version = "1.7.2-10.12.0.1024"
}

sourceSets.main{
    java{
        srcDirs 'src' //set the source folder as the /src subfolder
    }
    resources{
        srcDirs 'resources' //set the resources folder as the /resources subfolder
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    appendix = 'src'
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    appendix = 'deobf'
}

artifacts {
    //archives sourceJar
    archives deobfJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file://localhost/tmp/mvn/")
            addFilter('deobf') { artifact, file -> artifact.name == 'lib-colladamodel-deobf' }
        }
    }
}

processResources
{
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
